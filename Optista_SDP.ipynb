{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "ea897af5",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Mosek, JuMP, OffsetArrays, LinearAlgebra , MosekTools, Ipopt, Printf"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "291d4c28",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "solve_inner_primal (generic function with 1 method)"
      ]
     },
     "execution_count": 2,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "## elementary operations\n",
    "\n",
    "function e_i(n, i)\n",
    "    e_i_vec = zeros(n, 1)\n",
    "    e_i_vec[i] = 1\n",
    "    return e_i_vec\n",
    "end\n",
    "\n",
    "function ⊙(a,b)\n",
    "    return ((a*b') .+ transpose(a*b')) ./ 2\n",
    "end\n",
    "\n",
    "## stepsize of proximal gradient method\n",
    "\n",
    "function construct_GD_stepsizes(N)\n",
    "    α = OffsetArray(zeros(N, N), 1:N, 0:N-1)\n",
    "\n",
    "    for i in 1:N\n",
    "        for j in 0:i-1\n",
    "            α[i,j]=1\n",
    "        end\n",
    "    end\n",
    "     \n",
    "    return α   \n",
    "end\n",
    "\n",
    "## θ sequence\n",
    "\n",
    "function get_theta(N)\n",
    "    theta=OffsetArray(zeros(N+1), 0:N)\n",
    "    theta[0]=1\n",
    "    for i in 1:N-1\n",
    "        theta[i]=(1+sqrt(1+4*theta[i-1]^2))/2\n",
    "    end\n",
    "    theta[N]=(1+sqrt(1+8*theta[N-1]^2))/2\n",
    "    return theta\n",
    "end\n",
    "\n",
    "## γ sequence\n",
    "\n",
    "function get_gamma(N)\n",
    "    γ = OffsetArray(zeros(N), 0:N-1)\n",
    "    theta = get_theta(N)  \n",
    "    for i in 0:N-1\n",
    "        γ[i] = (2 * theta[i] / theta[N]^2) * (theta[N]^2 - 2 * theta[i]^2 + theta[i])\n",
    "    end\n",
    "\n",
    "    return γ\n",
    "end\n",
    "\n",
    "## construct OGM stepsizes\n",
    "\n",
    "function ogm_stepsizes(N) \n",
    "    theta=get_theta(N)\n",
    "    α=OffsetArray(zeros(N,N), 1:N, 0:N-1)\n",
    "    for i in 0:N-1\n",
    "        for k in 0:i-1\n",
    "            if k+1<=i\n",
    "                α[i+1,k]=α[k+1,k]+sum(2*theta[k]/theta[l+1]-(1/theta[l+1])*α[l,k] for l in k+1:i)\n",
    "            end\n",
    "        end\n",
    "        α[i+1,i]=1+(2*theta[i]-1)/theta[i+1]\n",
    "    end\n",
    "    return α\n",
    "end\n",
    "\n",
    "\n",
    "## construct OptISTA stepsizes\n",
    "\n",
    "function construct_optista_stepsizes(N)\n",
    "    α = ogm_stepsizes(N)\n",
    "    β = ogm_stepsizes(N)\n",
    "    ϕ = OffsetArray(zeros(N, N), 1:N, 0:N-1)\n",
    "    ψ = OffsetArray(zeros(N, N), 1:N, 0:N-1)\n",
    "\n",
    "    for i in 1:N\n",
    "        for j in 0:i-1\n",
    "            ϕ[i,j]=α[N,j]\n",
    "            ψ[i,j]=α[N,j]\n",
    "        end\n",
    "    end\n",
    "     \n",
    "    return ϕ,ψ,α,β\n",
    "end\n",
    "\n",
    "## data generations\n",
    "\n",
    "function data_generator_function(N, α, β, ϕ, ψ, L)\n",
    "\n",
    "    dim = (2*N) + 3\n",
    "    N_pts = (2*N) + 2\n",
    "\n",
    "    𝐰_star = zeros(dim, 1)\n",
    "    𝐰_0 = e_i(dim, 1)\n",
    "\n",
    "    #create gradients\n",
    "    \n",
    "    𝐟′ = OffsetArray(zeros(dim, N_pts), 1:dim, -1:2*N)  \n",
    "    𝐠′ = OffsetArray(zeros(dim, N_pts), 1:dim, -1:2*N)\n",
    "\n",
    "    # fill the elements of 𝐟′\n",
    "\n",
    "    for i in -1:N\n",
    "        𝐟′[:,i] = e_i(dim, i+3)\n",
    "    end\n",
    "\n",
    "    # fill the elements of 𝐠′\n",
    "\n",
    "    𝐠′[:,-1] = -𝐟′[:,-1]\n",
    "\n",
    "    for i in 1:N\n",
    "       𝐠′[:,N+i] = e_i(dim, N+3+i)\n",
    "    end\n",
    "\n",
    "    #create function evaluations\n",
    "\n",
    "    𝐟 = OffsetArray(zeros(dim, N_pts), 1:dim, -1:(2*N))\n",
    "    𝐠 = OffsetArray(zeros(dim, N_pts), 1:dim, -1:(2*N))\n",
    "\n",
    "    # fill elements of 𝐟\n",
    "\n",
    "    for i in -1:N\n",
    "        𝐟[:,i] = e_i(dim, i+2)\n",
    "    end\n",
    "\n",
    "    # fill elements of 𝐠\n",
    "\n",
    "    𝐠[:, -1] = e_i(dim, N+3)\n",
    "\n",
    "    for i in 1:N\n",
    "        𝐠[:, N+i] = e_i(dim, N+3+i)\n",
    "    end\n",
    "\n",
    "    #make 𝐰\n",
    "    \n",
    "    𝐰 = OffsetArray(Matrix{Any}(undef, dim, N_pts), 1:dim, -1:2*N)\n",
    "\n",
    "    𝐰[:,-1] = 𝐰_star\n",
    "\n",
    "    𝐰[:, 0] = 𝐰_0\n",
    "\n",
    "    for i in 1:N\n",
    "        𝐰[:,N+i] = 𝐰[:, 0] - (1/L)*sum(ϕ[i,j]*𝐟′[:,j] for j in 0:i-1) - (1/L)*sum(ψ[i,j]*𝐠′[:,N+j+1] for j in 0:i-1)\n",
    "    end\n",
    "    \n",
    "    for i in 1:N\n",
    "        𝐰[:, i] = 𝐰[:,0] - (1/L)*sum(α[i,j]*𝐟′[:,j] for j in 0:i-1) - (1/L)*sum(β[i,j]*𝐠′[:,N+j+1] for j in 0:i-1)\n",
    "    end\n",
    "\n",
    "    return 𝐰, 𝐟′, 𝐠′, 𝐟, 𝐠\n",
    "\n",
    "end\n",
    "\n",
    "##define matrices\n",
    "function A_mat_f(i, j, α, β, ϕ, ψ, 𝐟′, 𝐰)  \n",
    "    return ⊙(𝐟′[:,j], 𝐰[:,i] - 𝐰[:,j])\n",
    "end\n",
    "\n",
    "function A_mat_g(i, j, α, β, ϕ, ψ, 𝐠′, 𝐰)  \n",
    "    return ⊙(𝐠′[:,j], 𝐰[:,i] - 𝐰[:,j])\n",
    "end\n",
    "\n",
    "function B_mat(i, j, α, β, ϕ, ψ, 𝐰)     \n",
    "    return ⊙(𝐰[:,i] - 𝐰[:,j], 𝐰[:,i] - 𝐰[:,j])\n",
    "end\n",
    "\n",
    "function C_mat_f(i, j, 𝐟′)\n",
    "    return ⊙(𝐟′[:,i]-𝐟′[:,j], 𝐟′[:,i]-𝐟′[:,j])\n",
    "end\n",
    "\n",
    "function a_vec_f(i, j, 𝐟)\n",
    "    return 𝐟[:,j]-𝐟[:,i]\n",
    "end\n",
    "\n",
    "function a_vec_g(i, j, 𝐠)\n",
    "    return 𝐠[:,j]-𝐠[:,i]\n",
    "end\n",
    "\n",
    "\n",
    "## indices\n",
    "\n",
    "struct i_j_idx \n",
    "    i::Int64 \n",
    "    j::Int64 \n",
    "end\n",
    "\n",
    "\n",
    "function index_set_constructor(N)\n",
    "\n",
    "    # construct idx_f\n",
    "    idx_f = [i for i in -1:N]\n",
    "\n",
    "    # construct idx_g\n",
    "    idx_g = [-1; [N+i for i in 1:N]]\n",
    "\n",
    "    # construct the index set for λ\n",
    "    idx_set_λ = i_j_idx[]\n",
    "    for i in idx_f\n",
    "        for j in idx_f\n",
    "            if i!=j\n",
    "                push!(idx_set_λ, i_j_idx(i,j))\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "\n",
    "    # construct the index set for τ\n",
    "    idx_set_τ = i_j_idx[]\n",
    "    for i in idx_g\n",
    "        for j in idx_g\n",
    "            if i!=j\n",
    "                push!(idx_set_τ, i_j_idx(i,j))\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "\n",
    "    return idx_set_λ, idx_set_τ\n",
    "\n",
    "end\n",
    "\n",
    "\n",
    "## primal SDP \n",
    "function solve_inner_primal(N, L, α, β, ϕ, ψ, R; show_output = :off)\n",
    "\n",
    "    𝐰, 𝐟′, 𝐠′, 𝐟, 𝐠 = data_generator_function(N, α, β, ϕ, ψ, L)\n",
    "\n",
    "    dim_G = (2*N)+3\n",
    "    dim_F = (2*N)+3\n",
    "    idx_set_λ, idx_set_τ = index_set_constructor(N)\n",
    "    \n",
    "    #model\n",
    "    model_primal = Model(optimizer_with_attributes(Mosek.Optimizer))\n",
    "\n",
    "    #variables\n",
    "\n",
    "    @variable(model_primal, F[1:dim_F])\n",
    "    @variable(model_primal, G[1:dim_G, 1:dim_G], PSD)\n",
    "\n",
    "    # objective\n",
    "\n",
    "    @objective(model_primal, Max, F'*(a_vec_f(-1, N, 𝐟) + a_vec_g(-1, 2*N, 𝐠)))\n",
    "\n",
    "    # constraints\n",
    "\n",
    "    @constraint(model_primal, tr(G*B_mat(0, -1, α, β, ϕ, ψ, 𝐰)) <= R^2)\n",
    "\n",
    "    for i_j_λ in idx_set_λ\n",
    "        @constraint(model_primal,  \n",
    "        F'*a_vec_f(i_j_λ.i, i_j_λ.j, 𝐟) + tr(G* ( A_mat_f(i_j_λ.i, i_j_λ.j, α, β, ϕ, ψ, 𝐟′, 𝐰) + (1/(2*L)) * C_mat_f(i_j_λ.i, i_j_λ.j, 𝐟′) ))  <= 0 )\n",
    "    end\n",
    "\n",
    "    for i_j_τ in idx_set_τ\n",
    "        @constraint(model_primal,\n",
    "        F'*a_vec_g(i_j_τ.i, i_j_τ.j, 𝐠) +tr(G*A_mat_g(i_j_τ.i, i_j_τ.j, α, β, ϕ, ψ, 𝐠′, 𝐰)) <= 0)\n",
    "    end\n",
    "\n",
    "\n",
    "    #optimize\n",
    "\n",
    "    set_silent(model_primal)\n",
    "    optimize!(model_primal)\n",
    "\n",
    "    # store and return \n",
    "    obj_val_primal = objective_value(model_primal)\n",
    "    G_star = value.(G)\n",
    "    F_star = value.(F)\n",
    "\n",
    "    @info \"performance measure of primal is $(obj_val_primal)\"\n",
    "    return obj_val_primal, G_star, F_star\n",
    "\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "627a13e6",
   "metadata": {},
   "outputs": [
    {
     "name": "stderr",
     "output_type": "stream",
     "text": [
      "┌ Info: performance measure of primal is 0.3475053883064591\n",
      "└ @ Main In[2]:267\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "(0.3475053883064591, [8.999999929267846 0.010832212201259791 … 0.6841703191083359 0.6841705145085188; 0.010832212201259791 0.04187172254114919 … -0.04103488926245005 -0.041034892407911874; … ; 0.6841703191083359 -0.04103488926245005 … 0.09386790801059927 0.09386792143060715; 0.6841705145085188 -0.041034892407911874 … 0.09386792143060715 0.09386794386358055], [-0.44586023217820453, 0.7394132335083513, 0.19202287032529236, -0.0344839733283084, -0.14685053886746377, -0.12804422288217318, -0.17619710400231323, -0.1583242196853017, 0.29730260231481004, 0.08314801272725211, -0.04614846926383797, -0.09549599490409498, -0.08048195955473392])"
      ]
     },
     "execution_count": 3,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "# PEP result\n",
    "\n",
    "N=5\n",
    "L=2\n",
    "R=3\n",
    "\n",
    "ϕ,ψ,α,β = construct_optista_stepsizes(N)\n",
    "\n",
    "## primal\n",
    "obj_val_primal, G_star_primal, F_star_primal = solve_inner_primal(N, L, α, β, ϕ, ψ, R; show_output = :off)\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "eb055033",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "*** worst-case performance of OptISTA ***\n",
      "\t PEP guarantee:\t F(y_N)-F_* <= 0.347505 ||x0 - xs||^2\n",
      "\t Theorem 1:\t F(y_N)-F_* <= 0.347505 ||x0 - xs||^2\n"
     ]
    }
   ],
   "source": [
    "# Theorem 1\n",
    "\n",
    "θ = get_theta(N)\n",
    "\n",
    "convergence = L * R^2 / (2*θ[N]^2 - 2)\n",
    "\n",
    "println(\"*** worst-case performance of OptISTA ***\")\n",
    "@printf(\"\\t PEP guarantee:\\t F(y_N)-F_* <= %.6f ||x0 - xs||^2\\n\", obj_val_primal)\n",
    "@printf(\"\\t Theorem 1:\\t F(y_N)-F_* <= %.6f ||x0 - xs||^2\\n\", convergence)\n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.7.3",
   "language": "julia",
   "name": "julia-1.7"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.7.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
